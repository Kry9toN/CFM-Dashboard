plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
}

def gitCommitCount = "git rev-list HEAD --count".execute().text.trim()
def gitCommitHash = "git rev-parse --verify --short HEAD".execute().text.trim()

Properties properties = new Properties()
if (project.rootProject.file("local.properties").exists()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
}

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"
    ndkVersion "22.1.7171670"

    defaultConfig {
        applicationId "com.dashboard.kotlin"
        minSdkVersion 26
        targetSdkVersion 32
        versionCode gitCommitCount.toInteger()
        versionName "5.3"
        versionNameSuffix = ".r${gitCommitCount}.${gitCommitHash}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild{
            cmake{
                cppFlags "-std=c++20"
                abiFilters 'arm64-v8a'
            }
        }
        ndk {
            abiFilters 'arm64-v8a'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "$rootProject.name-v${variant.versionName}-${variant.buildType.name}.apk"
        }
    }

    signingConfigs {
        sign {
            storeFile properties.getProperty("storeFile") != null ? file(properties.getProperty("storeFile")) : android.signingConfigs.debug.storeFile
            storePassword properties.getProperty("storePassword", android.signingConfigs.debug.storePassword)
            keyAlias properties.getProperty("keyAlias", android.signingConfigs.debug.keyAlias)
            keyPassword properties.getProperty("keyPassword", android.signingConfigs.debug.keyPassword)
        }
    }
    
    buildTypes {
        release {
            signingConfig signingConfigs.sign
            minifyEnabled true
            shrinkResources true
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),'proguard-rules.pro'
        }
        debug {}
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    externalNativeBuild {
        cmake {
            version = "3.18.1"
            path file('./src/cpp/CMakeLists.txt')
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    //mmkv
    implementation 'com.tencent:mmkv-static:1.2.13'

    def libsuVersion = '4.0.3'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:busybox:${libsuVersion}"
}
